set(dir ${CMAKE_CURRENT_SOURCE_DIR})
set(FTH_SOURCES
    ${dir}/fortran_test_helper.f90
    ${dir}/fth_asserts.f90
    ${dir}/fth_arrays.f90
)
set(FTH_SOURCES ${FTH_SOURCES} PARENT_SCOPE)

add_library(${PROJECT_NAME} ${FTH_SOURCES})

set_target_properties(
    ${PROJECT_NAME}
    PROPERTIES
    POSITION_INDEPENDENT_CODE ON
    WINDOWS_EXPORT_ALL_SYMBOLS ON
)

set(FTH_MOD_DIR ${CMAKE_CURRENT_BINARY_DIR}/mod_files/)
if (NOT EXISTS "${FTH_MOD_DIR}")
    file(MAKE_DIRECTORY "${FTH_MOD_DIR}")
endif()

set_target_properties(
    ${PROJECT_NAME} PROPERTIES
    Fortran_MODULE_DIRECTORY ${FTH_MOD_DIR}
)
target_include_directories(
    ${PROJECT_NAME} PUBLIC
    $<BUILD_INTERFACE:${FTH_MOD_DIR}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_MODULEDIR}>
)

install(
    TARGETS ${PROJECT_NAME}
    EXPORT ${PROJECT_NAME}-targets
    RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
    ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
    LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)
install(DIRECTORY ${FTH_MOD_DIR} DESTINATION "${CMAKE_INSTALL_MODULEDIR}")
